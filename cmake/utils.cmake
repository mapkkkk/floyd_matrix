function(getSubDir SUB_DIRS DIR)
    file(GLOB DIR_FILES LIST_DIRECTORIES true RELATIVE ${DIR} ${DIR}/*)
    foreach(DIR_FILE ${DIR_FILES})
        if(IS_DIRECTORY ${DIR}/${DIR_FILE} AND EXISTS ${DIR}/${DIR_FILE}/CMakeLists.txt)
            list(APPEND ${SUB_DIRS} ${DIR_FILE})
        endif()
    endforeach()
    return(PROPAGATE ${SUB_DIRS})
endfunction()

function(getFiles FILES DIR REGEX)
    file(GLOB DIR_FILES LIST_DIRECTORIES false ${DIR}/*)
    foreach(DIR_FILE ${DIR_FILES})
        if(${DIR_FILE} MATCHES ${REGEX})
            list(APPEND ${FILES} ${DIR_FILE})
        endif()
    endforeach()
    list(SORT ${FILES} COMPARE NATURAL)
    return(PROPAGATE ${FILES})
endfunction()

function(getCaseSize SIZE FILE_PATH)
    cmake_path(GET FILE_PATH STEM FILE_NAME)
    string(REGEX MATCH "[0-9]+" ${SIZE} ${FILE_NAME})
    return(PROPAGATE ${SIZE})
endfunction()

function(getCheckFiles CHECK_FILES INPUT_FILES)
    foreach(INPUT_FILE ${${INPUT_FILES}})
        string(REPLACE "raw" "check" CHECK_FILE ${INPUT_FILE})
        if(EXISTS ${CHECK_FILE})
            list(APPEND ${CHECK_FILES} ${CHECK_FILE})
        else()
            message(WARNING "Check file of input file ${INPUT_FILE} does not exist, skipping.")
            list(REMOVE_ITEM ${INPUT_FILES} ${INPUT_FILE})
        endif()
    endforeach()
    return(PROPAGATE ${CHECK_FILES} ${INPUT_FILES})
endfunction()